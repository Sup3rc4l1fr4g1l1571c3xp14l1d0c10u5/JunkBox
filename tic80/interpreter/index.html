
<!DOCTYPE html>
<html>
<head>
    <style>
        html, body {
            width: 100vw;
            height: 100vh;
        }

        html, body, div, textarea, a {
            margin: 0;
            padding: 0;
            border: 0;
            outline: 0;
            font-size: 100%;
            vertical-align: baseline;
            background: transparent;
            -moz-box-sizing: border-box;
            box-sizing: border-box;
            display: inline;
        }

        a {
            background-color: lightgray;
        }

        textarea {
            resize: none;
            outline: 0;
            border: 1px solid lightgray;
        }
    </style>
    <script type="text/peg-js">
Program
  = stmts:StatementList _ { return new Program(stmts); }

StatementList
  = Statement*

Statement
  = Declaration
  / IfStatement
  / IterationStatement
  / SwitchStatement
  / BreakStatement
  / ContinueStatement
  / ReturnStatement
  / BlockStatement
  / ExpressionStatement
  / EmptyStatement

EmptyStatement
  = _ ";" { return new EmptyStatement(); }
  
IfStatement
  = _ "if" _ "(" _ condExpr:Expression _ ")" _ thenStmt:Statement _ "else" _ elseStmt:Statement { return new IfStatement( condExpr, thenStmt, elseStmt ); }
  / _ "if" _ "(" _ condExpr:Expression _ ")" _ thenStmt:Statement                               { return new IfStatement( condExpr, thenStmt, null     ); }

IterationStatement
  = _ "do" _ bodyStmt:Statement _ "while" _ "(" _ condExpr:Expression _ ")" _ ";" { return new DoStatement(condExpr, bodyStmt); }
  / _ "while" _ "(" _ condExpr:Expression _ ")" _ bodyStmt:Statement              { return new WhileStatement(condExpr, bodyStmt); }
  / _ "for" _ "(" _ initExpr:LexicalDeclaration _ condExpr:Expression? _ ";" _ updateExpr:Expression? _ ")" _ bodyStmt:Statement { return new ForStatement(initExpr, condExpr, updateExpr, bodyStmt); }
  / _ "for" _ "(" _ initExpr:Expression? _  ";" _ condExpr:Expression? _ ";" _ updateExpr:Expression? _ ")" _ bodyStmt:Statement { return new ForStatement(initExpr, condExpr, updateExpr, bodyStmt); }

LexicalDeclaration
  = _ "let" _ binds:LexicalBindings _ ";" { return new LexicalDeclaration(binds); }
  
LexicalBindings
  = x:LexicalBinding xs:(_ "," _ binding:LexicalBinding { return binding; })* { return [x].concat(xs); }

LexicalBinding
  = ident:BindingIdentifier _ "=" _ initExpr:AssignmentExpression { return new LexicalBinding(ident, initExpr); }
  / ident:BindingIdentifier                                       { return new LexicalBinding(ident, null); }

BindingIdentifier
  = Identifier
  
SwitchStatement
  = _ "switch" _ "(" _ expr:Expression _ ")" _ "{" _ blocks:CaseBlock _ "}" { return new SwitchStatement(expr, blocks); }

CaseBlock
  =  clauses:CaseClause+ _ stmt:Statement  { return new CaseBlock(clauses, stmt); }

CaseClause
  = _ "case" _ expr:Expression _ ":" { return new CaseClause(expr); }
  / _ "default" _ ":"           { return new DefaultClause(); }

BreakStatement
  = _ "break" _ ";" { return new BreakStatement(); }

ContinueStatement
  = _ "continue" _ ";" { return new ContinueStatement(); }

ReturnStatement
  = _ "return" _ expr:Expression _ ";" { return new ReturnStatement(expr); }
  / _ "return" _                   ";" { return new ReturnStatement(null); }

BlockStatement
  = _ "{" _ stmts:StatementList _ "}"  { return new BlockStatement(stmts); }

ExpressionStatement
  = expr:Expression _ ";" { return new ExpressionStatement(expr); }
  
Expression
  = CommaExpression

CommaExpression
  = x:AssignmentExpression xs:(_ "," _ e:AssignmentExpression { return e; })* { return new CommaExpression([x].concat(xs)); }
  
AssignmentExpression
  = ArrowFunction
  / lhs:LeftHandSideExpression _ op:AssignmentOperator _ rhs:AssignmentExpression { return new AssignmentExpression(op, lhs, rhs); }
  / ConditionalExpression

AssignmentOperator
  = "*=" / "/=" / "%=" / "+=" / "-=" / "<<=" / ">>=" / ">>>=" / "<<<=" / "&=" / "^=" / "|=" / "**=" / ((!"=>") "=")

ArrowFunction
  = "(" _ params:FunctionParameters? _ ")" _ "=>" _ body:ConciseBody { return new FunctionExpression(params, body); }

ConciseBody
  = FunctionBody
  / expr:AssignmentExpression  { return new ReturnStatement(expr); }

FunctionBody
  = BlockStatement

Declaration
  = LexicalDeclaration
  / FunctionDeclaration
  
FunctionDeclaration
  = _ "function" _ ident:BindingIdentifier _ "(" _ params:FunctionParameters? _ ")" _ body:FunctionBody
  { return new LexicalDeclaration([new LexicalBinding(ident, new FunctionExpression(params, body))]); }

FunctionParameters
  = xs:BindingElements x:(_ "," _ e:BindingRestElement { return e; } )? { return x ? xs + [x] : xs; }
  / BindingRestElement

BindingElements
  = x:BindingElement xs:(_ "," _ e:BindingElement { return e; })* { return [x].concat(xs); }

BindingElement
  = name:BindingIdentifier _ "=" _ init:AssignmentExpression { return new FunctionParameter( name, init, false); }
  / name:BindingIdentifier                                   { return new FunctionParameter( name, null, false); }

BindingRestElement
  = "..." _ name:BindingIdentifier { return new FunctionParameter( name, null, true);  }

ConditionalExpression 
  = condExpr:LogicalORExpression _ "?" _ thenExpr:AssignmentExpression _ ":" _ elseExpr:AssignmentExpression { return new ConditionalExpression(condExpr, thenExpr, elseExpr); }
  / LogicalORExpression 

LogicalORExpression  = e1:LogicalANDExpression e2:(_ "||" _ LogicalANDExpression)* { return e2.reduce((s,x) => new LogicalORExpression(s, x[3]), e1); }
LogicalANDExpression = e1:BitwiseORExpression  e2:(_ "&&" _ BitwiseORExpression)*  { return e2.reduce((s,x) => new LogicalANDExpression(s, x[3]), e1); }
BitwiseORExpression  = e1:BitwiseXORExpression e2:(_ "|" _ BitwiseXORExpression)*  { return e2.reduce((s,x) => new BitwiseORExpression(s, x[3]), e1); }
BitwiseXORExpression = e1:BitwiseANDExpression e2:(_ "^" _ BitwiseANDExpression)*  { return e2.reduce((s,x) => new BitwiseXORExpression(s, x[3]), e1); }
BitwiseANDExpression = e1:EqualityExpression   e2:(_ "&" _ EqualityExpression)*    { return e2.reduce((s,x) => new BitwiseANDExpression(s, x[3]), e1); }
EqualityExpression = e1:RelationalExpression   e2:(_ EqualityOperator _ RelationalExpression)* { return e2.reduce((s,x) => new EqualityExpression(x[1], s, x[3]), e1); }
EqualityOperator = "==" / "!="
RelationalExpression = e1:ShiftExpression e2:(_ RelationalOperator _ ShiftExpression)* { return e2.reduce((s,x) => new RelationalExpression(x[1], s, x[3]), e1); }
RelationalOperator = ">=" / ">" / "<=" / "<"

ShiftExpression = e1:AdditiveExpression e2:(_ ShiftOperator _ AdditiveExpression)* { return e2.reduce((s,x) => new ShiftExpression(x[1], s, x[3]), e1); }
ShiftOperator = "<<<" / "<<" / ">>>" / ">>"

AdditiveExpression = e1:MultiplicativeExpression e2:(_ AdditiveOperator _ MultiplicativeExpression)* { return e2.reduce((s,x) => new AdditiveExpression(x[1], s, x[3]), e1); }
AdditiveOperator "AdditiveOperator" = "+" / "-"

MultiplicativeExpression = e1:ExponentiationExpression e2:(_ MultiplicativeOperator _ ExponentiationExpression)* { return e2.reduce((s,x) => new MultiplicativeExpression(x[1], s, x[3]), e1); }
MultiplicativeOperator "MultiplicativeOperator" = "*" / "/"

ExponentiationExpression = e1:UnaryExpression e2:(_ "**" _ UnaryExpression)* { return e2.reduce((s,x) => new ExponentiationExpression(s, x[3]), e1); }

UnaryExpression  = op:UnaryOperator _ e:UnaryExpression { return new UnaryExpression(op,e); }
                 / LeftHandSideExpression
UnaryOperator = "+" / "-" / "~" / "!"

LeftHandSideExpression
  = e:PrimaryExpression es:(CallExpression / MemberExpressionPost)* { return es.reduce((s,x) => (x.lhs = s, x), e); }

CallExpression
  = args:Arguments { return new CallExpression(null, args); }

Arguments
  = _ "(" _ x: Expression xs:( _ "," _ e:Expression { return e; })* _ ")" { return [x].concat(xs); }
  / _ "("  _ ")" { return []; }

MemberExpression
  = e:PrimaryExpression es:MemberExpressionPost * { return es.reduce((s,x) => (x.lhs = s, x), e); }

MemberExpressionPost
  = _ "[" _ expr:Expression _ "]" { return new ArrayIndexExpression(null, expr); }
  / _ "." _ name:Identifier { return new ArrayIndexExpression(null, name); }

PrimaryExpression
  = Identifier
  / Literal
  / ArrayLiteral
  / ObjectLiteral
  / FunctionExpression
  / _ "(" _ expr:Expression _ ")" { return new EnclosedInParenthesesExpression(expr); }

Literal
  = NullLiteral
  / BooleanLiteral
  / NumericLiteral
  / StringLiteral

ArrayLiteral  = _ "[" _ v:ArrayLiteralList _ "]" { return new ArrayLiteral(v); }
              / _ "[" _ "]" { return new ArrayLiteral([]); }
ArrayLiteralList = x:AssignmentExpression xs:(_ "," _ e:AssignmentExpression { return e; })* { return [x].concat(xs); }

ObjectLiteral = _ "{" _ v:PropertyDefinitionList _ "}" { return new ObjectLiteral(v); }
              / _ "{" _ "}" { return new ObjectLiteral([]); }
PropertyDefinitionList = x:PropertyDefinition xs:(_ "," _ e:PropertyDefinition { return e; })* { return [x].concat(xs); }
PropertyDefinition = name:PropertyName _ ":" _ value:AssignmentExpression { return new PropertyDefinition(name, value); }
PropertyName
  = Identifier
  / StringLiteral
  / NumericLiteral

FunctionExpression  = _ "function" _ "(" _ params:FunctionParameters _ ")" _ "{" _ body:FunctionBody _ "}"
  { return new FunctionExpression(params, body); }

NullLiteral = _ "null" { return new NullLiteral(); }
BooleanLiteral = _ "true"  { return new BooleanLiteral(true); }
               / _ "false" { return new BooleanLiteral(false); }
NumericLiteral = _ number:([0-9]+ ( "." [0-9]+)?  { return new NumericLiteral(text()); }) { return number; }
StringLiteral = _ string:("\"" ([^\"] / "\\" [.])* "\"" { return new StringLiteral(text()); }) { return string; }

Identifier "identifier" = _ ident:([A-Za-z_][A-Za-z0-9_]* { return new IdentifierLiteral(text()); }) { return ident; }
_ "whitespace" = [ \t\n\r]*

    </script>
    <script src="./peg.js"></script>
    <script src="./app.js"></script>
    <script src="./jsDump.js"></script>
</head>
<body>
    <div style="display:flex; width:100vw; height:100vh; flex-direction: column; flex-wrap: wrap;">
        <div style="display:flex; "><a id="eval">eval</a></div>
        <div style="display:flex; flex:1; flex-direction: row; flex-wrap: wrap;">
            <div style="display:flex; flex:1;"><textarea id="code" style="width:100%; height:100%;"></textarea></div>
            <div style="display:flex; flex:1;"><textarea id="output" style="width:100%; height:100%;"></textarea></div>
        </div>
    </div>
</body>
</html>
